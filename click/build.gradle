plugins {
  id 'java-library'
  id 'idea'
  id 'maven-publish'

  id("io.freefair.lombok")   version "latest.release" // id 'io.franzbecker.gradle-lombok' version 'latest.release'
  id("name.remal.sonarlint") version "latest.release" // https://github.com/remal-gradle-plugins/sonarlint + https://lift.sonatype.com/
  id("net.ltgt.errorprone")  version "latest.release" // https://errorprone.info/
}

repositories {
  mavenLocal()
  mavenCentral()
}

javadoc {
  options.encoding = 'UTF-8'
  options.addBooleanOption('html5', true)
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
  options.compilerArgs.addAll(['-Xlint:all', '-parameters', '-g', '-Xmaxwarns', '999'])

  sourceCompatibility = 17 // for the IDE support
  targetCompatibility = 8
  options.release.set(8) // javac --release 7..18+
  options.deprecation = true
  javaCompiler.set( javaToolchains.compilerFor {
    languageVersion = JavaLanguageVersion.of(17)
  })

  options.errorprone {
    enabled = true
    disableWarningsInGeneratedCode = true
    excludedPaths = ".*/(generated|test).*/.*"
    disable("MissingSummary")
    disable("ParameterName")
    disable("UnusedVariable")
    errorproneArgs = ["--illegal-access=warn"]
  }
}
// https://docs.gradle.org/current/userguide/publishing_maven.html
publishing {
  publications { // artifactId == folder name
    maven(MavenPublication) {
      from components.java
    }
  }
}
java {
  withSourcesJar()
//  withJavadocJar()
}

dependencies {
  errorprone "com.google.errorprone:error_prone_core:latest.release"
  api("com.google.code.findbugs:jsr305:latest.release")// https://javadoc.io/doc/com.google.code.findbugs/jsr305/latest/index.html
  api("com.google.errorprone:error_prone_annotations:latest.release")// https://javadoc.io/doc/com.google.errorprone/error_prone_annotations/latest/index.html
  //implementation("javax.annotation:javax.annotation-api:latest.release")

  // Java 8 support
  annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:latest.release' // 0.4.2
  compileOnly 'com.github.bsideup.jabel:jabel-javac-plugin:latest.release'

  implementation 'javax.servlet:servlet-api:2.5'

  api('org.slf4j:slf4j-api:1.7.+',"org.slf4j:jcl-over-slf4j:1.7.+","org.slf4j:log4j-over-slf4j:1.7.+")

  api 'org.mvel:mvel2:latest.release'// 2.4.14
  api("com.github.ben-manes.caffeine:caffeine:2.+")
  api('com.google.guava:guava:31.1-jre'){
    exclude group: 'com.google.auto.value', module: 'auto-value-annotations'
  }
  implementation 'org.mvel:mvel2:latest.release'// 2.4.14
  implementation("com.github.ben-manes.caffeine:caffeine:2.+") // 3.x requires Java 11+

  api 'org.apache.commons:commons-lang3:3.+'
  api('commons-lang:commons-lang:2.+')
  api 'commons-io:commons-io:2.+'
  api('commons-collections:commons-collections:3.+')
  api('commons-fileupload:commons-fileupload:1.3.+'){
    exclude group: "commons-io", module: "commons-io"
  }

  //*** TEST TEST TEST

  testImplementation 'junit:junit:4.+'
  testImplementation 'org.mockito:mockito-inline:latest.release'

  testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.+'

  testImplementation project(':click-mock')
  testImplementation project(':click-ognl')
  testImplementation project(path: ':click-groovy-ps') // groovy is fast and can set private fields, but have problems with type conversions
  testImplementation project(path: ':click-extras')
  testImplementation project(':click-velocity')
}

// https://tomgregory.com/how-to-exclude-gradle-dependencies/
configurations.all { // .implementation
  exclude group: "velocity", module: "velocity" // 1.3 they come from somewhere..
  exclude group: "commons-logging", module: "commons-logging"
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

compileTestJava {
  sourceCompatibility = 17
  targetCompatibility = 8
}

test {
  useJUnit()
  testLogging {
    events "passed", "skipped", "failed"
    showStandardStreams = true // show standard out & err of the test JVM on the console
    showExceptions = true
    exceptionFormat = 'full'
  }
  enableAssertions = true
  maxHeapSize = "500m"

  System.setProperty("slf4j.detectLoggerNameMismatch", "true")
  System.setProperty("file.encoding", "UTF-8")
  System.setProperty("user.language", "en")
  Locale.setDefault(new Locale("en", "US"));
}
lombok {
  version = "latest.release"
}
sonarLint {
  ignoreFailures = true
}