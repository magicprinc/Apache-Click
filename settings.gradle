// https://docs.gradle.org/7.6/userguide/multi_project_builds.html
// https://docs.gradle.org/7.6/samples

rootProject.name = 'click'

// https://docs.gradle.org/current/userguide/platforms.html
dependencyResolutionManagement {
  versionCatalogs {
    libs {
      version('springVersion', "3.+")
      version("tomcatVersion", "8.0.+")

      library("spring-code", "org.springframework", "spring-core").versionRef("springVersion")

      library("junit", "junit", "junit").version("4.+")
      library("mockito", "org.mockito", "mockito-inline").version("latest.release")

      library("tomcat-core",   "org.apache.tomcat.embed", "tomcat-embed-core").versionRef("tomcatVersion")
      library("tomcat-juli",   "org.apache.tomcat.embed", "tomcat-embed-logging-juli").versionRef("tomcatVersion")
      library("tomcat-jasper", "org.apache.tomcat.embed", "tomcat-embed-jasper").versionRef("tomcatVersion")

      //*** bundles ***
      bundle("spring", ["spring-code"])
      bundle("tomcat", ["tomcat-core", "tomcat-juli", "tomcat-jasper"])
      bundle("junit", ["junit", "mockito"])

      //*** libs.plugins ***
      plugin("lombok", "io.freefair.lombok").version("6.+") //old: id 'io.franzbecker.gradle-lombok' version 'latest.release'
      plugin("sonarLint", "name.remal.sonarlint").version("3.0.+") // https://github.com/remal-gradle-plugins/sonarlint + https://lift.sonatype.com/ + https://www.sonarsource.com/products/sonarlint/
      plugin("errorProne", "net.ltgt.errorprone").version("3.0.+") // https://errorprone.info/ + https://github.com/tbroyer/gradle-errorprone-plugin

      plugin("tomcat", "com.bmuschko.tomcat").version("2.+") // https://github.com/bmuschko/gradle-tomcat-plugin + https://plugins.gradle.org/plugin/com.bmuschko.tomcat
      //^?or:  id("org.gretty") version "4.0.+" // https://gretty-gradle-plugin.github.io/gretty-doc/
    }
  }
}

include 'click'
include 'click-cayenne'
include 'click-extras'
include 'click-freemarker'

include 'click-hibernate'
include 'click-mock'
include 'click-ognl'
include 'click-velocity'
include 'examples'
include 'template'

include 'click-charts'
include 'click-charts-examples'

include 'click-jquery'
include 'click-jquery-examples'

include 'extra-clickclick'
include 'extra-clickclick-examples'

include 'extra-click-charts-enhanced'
include 'extra-click-charts-enhanced-examples'

include 'click-jpa-demo'

include 'extra-click-calendar'
include 'extra-click-calendar-examples'

include 'extra-ajax4click'
include 'extra-ajax4click-examples'

include 'click-groovy-ps'